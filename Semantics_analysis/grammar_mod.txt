/* Program */
S       -> program
program -> basic id() block
block   -> { decls stmts }

/* Declarations */
decls   -> decl decls
decls   -> ε
decl    -> type id;
type    -> type[num]
type    -> basic
basic   -> float
basic   -> int
basic   -> double
basic   -> String
basic   -> char

/* Statements */
stmts   -> stmt stmts
stmts   -> ε
stmt    -> loc = expr;
stmt    -> if (bool) stmt
stmt    -> if (bool) stmt else stmt
stmt    -> while (bool) stmt
stmt    -> do stmt while (bool) ;
stmt    -> break;
stmt    -> block
loc     -> loc[num]
loc     -> id

/* Expressions */
bool    -> bool || join
bool    -> join
join    -> join && equality
join    -> equality
equality-> equality == rel
equality-> equality != rel
equality-> rel
rel     -> expr < expr
rel     -> expr <= expr
rel     -> expr >= expr
rel     -> expr > expr
rel     -> expr
expr    -> expr + term
expr    -> expr - term
expr    -> term
term    -> term * unary
term    -> term / unary
term    -> unary
unary   -> !unary
unary   -> -unary
unary   -> factor
factor  -> ( expr )
factor  -> loc
factor  -> id
factor  -> num
factor  -> real
factor  -> true
factor  -> false